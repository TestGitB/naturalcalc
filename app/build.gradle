apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        applicationId "com.nstudio.calc.casio"
        versionCode 13
        versionName "1.1.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'force-proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'force-proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        disable "RtlHardcoded", "RtlSymmetry", "RtlEnabled", "ApplySharedPref", "PrivateResource", "SetTextI18n"
    }
}

def Properties props = new Properties()
def propFile = new File('common/signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))
    android.signingConfigs.release.storeFile = file(props['RELEASE_STORE_FILE'])
    android.signingConfigs.release.storePassword = props['RELEASE_STORE_PASSWORD']
    android.signingConfigs.release.keyAlias = props['RELEASE_KEY_ALIAS']
    android.signingConfigs.release.keyPassword = props['RELEASE_KEY_PASSWORD']
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
    //throw new GradleException('signing.properties not found')
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile project(':common')
    compile project(':symja-release')
    compile 'com.android.support:multidex:1.0.2'
    compile 'com.github.mukeshsolanki:MarkdownView-Android:1.0.4'
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'io.github.kexanie.library:MathView:0.0.4'
    compile 'io.github.kobakei:ratethisapp:1.2.0'
}

apply plugin: 'com.google.gms.google-services'